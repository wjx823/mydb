;{*******************************************************************
; AI maccha Version 7.00 darkage.per
;}*******************************************************************
;{strategic-number---------------------------------------------------
(defrule 
   (true)
=>
   (set-strategic-number sn-cap-civilian-explorers 10)
   (set-strategic-number sn-cap-civilian-builders 2)
   (set-strategic-number sn-percent-civilian-explorers 50)
   (set-strategic-number sn-percent-civilian-builders 50)
   (set-strategic-number sn-percent-civilian-gatherers 0)
   (disable-self)
)
(defrule 
   (or
      (resource-found food)
      (unit-type-count 594 >= 6)
   )
=>
   (chat-local-to-self "stop finding food.")
   (set-strategic-number sn-cap-civilian-explorers 0)
   (set-strategic-number sn-cap-civilian-builders 10)
   (set-strategic-number sn-percent-civilian-explorers 0)
   (set-strategic-number sn-percent-civilian-builders 10)
   (set-strategic-number sn-percent-civilian-gatherers 90)
   (disable-self)
)
;}-------------------------------------------------------------------
;{villager adjustment------------------------------------------------
(defrule 
   (current-age == dark-age)
=>
   (set-strategic-number sn-food-gatherer-percentage 99) 
   (set-strategic-number sn-gold-gatherer-percentage  0) 
   (set-strategic-number sn-stone-gatherer-percentage 0) 
   (set-strategic-number sn-wood-gatherer-percentage  1) 
   (chat-local-to-self "villager adjustment in dark-age 1st 99/0/0/1")
   (disable-self) 
)
(defrule 
   (current-age == dark-age)
   (unit-type-count-total villager >= 8)
=>
   (set-strategic-number sn-food-gatherer-percentage 65) 
   (set-strategic-number sn-gold-gatherer-percentage  0) 
   (set-strategic-number sn-stone-gatherer-percentage 0) 
   (set-strategic-number sn-wood-gatherer-percentage 35) 
   (chat-local-to-self "villager adjustment in dark-age 2nd 65/0/0/35")
   (disable-self) 
)
(defrule 
   (current-age == dark-age)
   (unit-type-count-total villager >= 24)
=>
   (set-strategic-number sn-food-gatherer-percentage 55) 
   (set-strategic-number sn-gold-gatherer-percentage  0) 
   (set-strategic-number sn-stone-gatherer-percentage 0) 
   (set-strategic-number sn-wood-gatherer-percentage 45) 
   (chat-local-to-self "villager adjustment in dark-age 3rd 55/0/0/45")
   (disable-self) 
)
;}-------------------------------------------------------------------
;{Buiilding----------------------------------------------------------
(defrule
   (current-age == dark-age)
   (building-type-count house < 6)
   (housing-headroom < 4) 
   (can-build house) 
=>
   (build house) 
)
(defrule 
   (or(goal EVOLUTION 1)(current-age >= feudal-age))
   (building-type-count outpost == 0)
   (can-build outpost)
=>
   (chat-local-to-self "build outpost(dammy)")
   (set-strategic-number sn-minimum-town-size 30)
   (set-strategic-number sn-maximum-town-size 40)
   (build outpost)
)
(defrule 
   (goal EVOLUTION 1)
   (can-build barracks)
=>
   (chat-local-to-self "build first barracks")
   (set-strategic-number sn-minimum-town-size 10)
   (set-strategic-number sn-maximum-town-size 12)
   (build barracks)
   (disable-self)
)
;}-------------------------------------------------------------------
;{Building2----------------------------------------------------------
;*lumber-camp
(defrule  
   (game-time > 240)
   (resource-found wood)
   (building-type-count-total mill != 0)
   (can-build lumber-camp)
=>
   (chat-local-to-self "build lumber-camp <compulsion>")
   (set-strategic-number sn-camp-max-distance 18)
   (build lumber-camp)
   (disable-self)
)
(defrule  
   (current-age <= feudal-age)
   (building-type-count-total lumber-camp == 0) 
   (unit-type-count-total villager >= 15)
=>
   (chat-local-to-self "expansion of recognition range of forest")
   (set-strategic-number sn-maximum-wood-drop-distance 15)
   (disable-self)
)
(defrule 
   (building-type-count lumber-camp <= 1)
   (resource-found wood)
   (dropsite-min-distance wood > 5)
   (can-build-with-escrow lumber-camp)
=>
   (chat-local-to-self "build lumber-camp 1st")
   (release-escrow wood)
   (set-strategic-number sn-camp-max-distance 18)
   (build lumber-camp)
)
;*mining-camp
(defrule 
   (resource-found gold)
   (or
      (strategic-number sn-gold-gatherer-percentage > 0)
      (unit-type-count-total villager >= 28)
   )
   (dropsite-min-distance gold > 5)
   (can-build mining-camp)
=>
   (chat-local-to-self "build mining-camp for gold 1st")
   (set-strategic-number sn-camp-max-distance 20)
   (build mining-camp)
   (disable-self)
)

(defrule 
   (resource-found stone)
   (or
      (strategic-number sn-stone-gatherer-percentage > 0)
      (unit-type-count-total villager >= 28)
   )
   (dropsite-min-distance stone > 5)
   (can-build mining-camp)
=>
   (chat-local-to-self "build mining-camp for stone 1st")
   (release-escrow wood)
   (set-strategic-number sn-camp-max-distance 20)
   (build mining-camp)
   (disable-self)
)
;*mill
(defrule  
   (resource-found food) 
   (dropsite-min-distance food > 3)
   (can-build mill) 
=>
   (chat-local-to-self "build first mill") 
   (build mill) 
   (disable-self)
)
(defrule  
   (current-age == dark-age)
   (building-type-count-total mill == 0)
   (unit-type-count-total villager >= 12)
   (resource-found food) 
   (can-build mill) 
=>
   (chat-local-to-self "build first mill <compulsion>") 
   (build mill)
   (disable-self)
)
;*farm
(defrule
   (goal EVOLUTION 0)
   (building-type-count-total lumber-camp >= 1)
   (building-type-count farm < 16)
   (can-build farm)
=>
   (chat-local-to-self "build farm in dark-age")
   (build farm)
)
;}-------------------------------------------------------------------
;{upgrade------------------------------------------------------------
(defrule 
   (or
      (food-amount < 50)
      (population >= 24)
   )
   (can-research ri-loom)
=>
   (chat-local-to-self "research ri-loom") 
   (research ri-loom)
)
(defrule 
   (or
      (unit-type-count villager >= pop-50)
      (unit-type-count villager >= 30)
   )
   (can-research feudal-age)
=>
   (chat-local-to-self "research feudal-age") 
   (set-goal EVOLUTION 1)
   (research feudal-age)
   (set-strategic-number sn-food-gatherer-percentage 40)
   (set-strategic-number sn-gold-gatherer-percentage 7)
   (set-strategic-number sn-stone-gatherer-percentage 10)
   (set-strategic-number sn-wood-gatherer-percentage 43)
   (chat-local-to-self "villager adjustment in dark-age 4th 40/7/10/43")
)
;}-------------------------------------------------------------------
;{train villager-----------------------------------------------------
(defrule 
   (current-age == dark-age)
   (population-cap != 25)
   (unit-type-count villager < 32) 
   (can-train villager) 
=>
   (chat-local-to-self "train villager in dark-age s.t.population-cap != 25")
   (train villager)
)
(defrule 
   (current-age == dark-age)
   (population-cap == 25)
   (unit-type-count villager < 20) 
   (can-train villager) 
=>
   (chat-local-to-self "train villager in dark-age s.t.population-cap = 25")
   (train villager)
)
;}-------------------------------------------------------------------
